def getAppVersion = { ->
  try (final var gitTagOut = new ByteArrayOutputStream()) {
    exec {
      commandLine 'git', 'tag', '--points-at', 'HEAD'
      standardOutput = gitTagOut
    }
    final var tagName = gitTagOut.toString().strip()
    if (tagName.isBlank()) {
      try (final var gitHashOut = new ByteArrayOutputStream()) {
        exec {
          commandLine 'git', 'rev-parse', '--short', 'HEAD'
          standardOutput = gitHashOut
        }
        return 'git-' + gitHashOut.toString().strip()
      }
    } else {
      return tagName
    }
  }
}

ext {
  VERSION = getAppVersion()
  VCS_URL = 'https://github.com/sava-software/sava'
  SAVA = '1.10.0'
  SAVA_SERVICES = '0.4.3'
  JETTY = '12.0.14'
}

apply plugin: 'version-catalog'
apply plugin: 'maven-publish'

project.group = 'software.sava'
project.version = "$VERSION"

catalog {
  versionCatalog {
    // https://mvnrepository.com/artifact/org.junit.jupiter/junit-jupiter
    library("junit-jupiter", 'org.junit.jupiter:junit-jupiter:5.11.0')

    // https://www.bouncycastle.org/download/bouncy-castle-java/#latest
    library('bouncycastle', 'org.bouncycastle:bcprov-jdk18on:1.78.1')

    // https://github.com/comodal/json-iterator/tags
    library('json-iterator', 'systems.comodal:json-iterator:2.11.0')

    library('sava-core', "software.sava:sava-core:$SAVA")
    library('sava-rpc', "software.sava:sava-rpc:$SAVA")

    library('sava-solana-web2', 'software.sava:solana-web2:1.7.2')
    library('sava-solana-programs', 'software.sava:solana-programs:1.7.3')
    library('sava-anchor-src-gen', 'software.sava:anchor-src-gen:1.7.5')
    library('sava-anchor-programs', 'software.sava:anchor-programs:1.7.21')

    library('sava-core-services', "software.sava:core-services:$SAVA_SERVICES")
    library('sava-jetty-services', "software.sava:jetty-services:$SAVA_SERVICES")
    library('sava-solana-services', "software.sava:solana-services:$SAVA_SERVICES")

    // https://mvnrepository.com/artifact/org.eclipse.jetty/jetty-server
    library('jetty-server', "org.eclipse.jetty:jetty-server:$JETTY")
    // https://mvnrepository.com/artifact/org.eclipse.jetty.http2/jetty-http2-server
    library('jetty-http2-server', "org.eclipse.jetty.http2:jetty-http2-server:$JETTY")
    // https://mvnrepository.com/artifact/org.eclipse.jetty/jetty-alpn-server
    library('jetty-alpn-server', "org.eclipse.jetty:jetty-alpn-server:$JETTY")
    library('jetty-alpn-java-server', "org.eclipse.jetty:jetty-alpn-java-server:$JETTY")
    // https://mvnrepository.com/artifact/org.eclipse.jetty/jetty-alpn-conscrypt-server
    library('jetty-alpn-conscrypt-server', "org.eclipse.jetty:jetty-alpn-conscrypt-server:$JETTY")
    // https://mvnrepository.com/artifact/org.eclipse.jetty.http3/jetty-http3-server
    library('jetty-http3-server', "org.eclipse.jetty.http3:jetty-http3-server:$JETTY")
  }
}

publishing {
  publications {
    maven(MavenPublication) {
      from components.versionCatalog
      groupId project.group
      artifactId project.name
      version = project.version
    }
  }
  repositories {
    maven {
      name = "GithubPackages"
      url = "https://maven.pkg.github.com/sava-software/solana-version-catalog"
      credentials {
        username = System.getenv("GITHUB_ACTOR") ?: project.findProperty("gpr.user.write")
        password = System.getenv("GITHUB_TOKEN") ?: project.findProperty("gpr.token.write")
      }
    }
  }
}
